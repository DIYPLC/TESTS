FUNCTION_BLOCK "LGF_SetTime"
TITLE = LGF_SetTime
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Digital_Industry
FAMILY : LGF
NAME : LGF_SetTime
//This block combines the functions of system time, local time, and set time zone.
   VAR_INPUT 
      execute : Bool;   // Rising edge starts action once
      systemTime {InstructionName := 'DTL'; LibVersion := '1.0'} : DTL;   // System time to be set in PLC
      isLocalTime : Bool;   // TRUE: `systemTime` is local time, FALSE: `systemTime` is UTC time
      timeZone : Int;   // Timezones HHMM [-1200.. -330.. 0.. 930.. 1200.. 1300]
      isDaylightSavingTime : Bool;   // Daylight saving time changeover, TRUE: activated, FALSE: deactivated (more infos at "Adjusting parameters in the `statTimeZone` variable")
   END_VAR

   VAR_OUTPUT 
      done { ExternalWritable := 'False'} : Bool;   // TRUE: Commanded functionality has been completed successfully
      busy { ExternalWritable := 'False'} : Bool;   // TRUE: FB is active and new output values can be expected
      error { ExternalWritable := 'False'} : Bool;   // FALSE: No error / TRUE: An error occurred during the execution of the FB
      lastSetTimeZone { ExternalWritable := 'False'} : String;   // Time zone that was set last by this block
      status { ExternalWritable := 'False'} : Word := #STATUS_NO_CALL;   // 16#0000-16#7FFF: Status of the FB / 16#8000-16#FFFF: Error identification (see following Table)
      subFunctionStatus { ExternalWritable := 'False'} : Word;   // Status or return value of called FB's, FC's and system blocks
   END_VAR

   VAR 
      statExecuteOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Previous value of 'execute' input for edge detection
      statDone { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static value for output 'done'
      statBusy { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static value for output 'busy'
      statError { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static value for output 'error'
      statStatus { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Word := #STATUS_NO_CALL;   // Static value for output 'status'
      statSubFunctionStatus { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Word;   // Static value for output 'subFunxtionStatus'
      statFBState { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : DInt := #FB_STATE_NO_PROCESSING;   // State in the state machine of the FB
      instSetTimeZone {InstructionName := 'SET_TIMEZONE'; LibVersion := '2.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : SET_TIMEZONE;   // Instance SET_TIMEZONE: Set the parameter for the local time zone and the daylight saving / standard time changeover
      statTimeZone {InstructionName := 'TimeTransformationRule'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : TimeTransformationRule := (0, 60, 3, 5, 1, 2, 0, 10, 5, 1, 3, 0, 'not even set by LGF_TimeZone');   // Store the parameters for the local time zone and the daylight saving/standard time changeover 
   END_VAR

   VAR_TEMP 
      tempExecute : Bool;   // Temporary value for input 'execute'
      tempSetTimeZoneDone : Bool;   // Temporary value for initialization of SetTimeZone function - done
      tempSetTimeZoneBusy : Bool;   // Temporary value for initialization of SetTimeZone function - busy
      tempSetTimeZoneError : Bool;   // Temporary value for initialization of SetTimeZone function - error
      tempSetTimeZoneStatus : Word;   // Temporary value for initialization of SetTimeZone function - status
      tempResultSetTime : Word;   // Temporary value for status codes of WR_LOC_T and WR_SYS_T
   END_VAR

   VAR CONSTANT 
      FB_STATE_NO_PROCESSING : DInt := 0;   // FB state: No processing
      FB_STATE_SET_TIME : DInt := 1;   // FB state: Processing Set Time
      FB_STATE_SET_TIMEZONE : DInt := 2;   // FB state: Processing Set Time Zone
      STATUS_EXECUTION_FINISHED_NO_ERROR : Word := 16#0000;   // Execution finished without errors
      STATUS_NO_CALL : Word := 16#7000;   // No job being currently processed
      STATUS_FIRST_CALL : Word := 16#7001;   // First call after incoming new job (rising edge 'execute')
      STATUS_SUBSEQUENT_CALL : Word := 16#7002;   // Subsequent call during active processing without further details
      ERR_UNDEFINED_STATE : Word := 16#8600;   // Error due to an undefined state in state machine
      ERR_WRONG_TIMEZONE : Word := 16#8601;   // Error due to an undefined time zone
      ERR_SET_TIME_LOCAL : Word := 16#8201;   // Error instruction WR_LOC_T: Write local time, check `subFunctionStatus` code
      ERR_SET_TIME_UTC : Word := 16#8202;   // Error instruction WR_SYS_T: Set time-of-day, check `subFunctionStatus` code
      ERR_SET_TIMEZONE : Word := 16#8203;   // Error instruction SET_TIMEZONE, check subFunctionStatus code
      UTC_MINUS_1200 : Int := -1200;   // (UTC -12:00) Eniwetok, Kwajalein
      UTC_MINUS_1200_BIAS : Int := -720;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_1200_NAME : String := '(UTC -12:00)';   // Time zone name: (UTC -12:00) Eniwetok, Kwajalein
      UTC_MINUS_1100 : Int := -1100;   // (UTC -11:00) Midway Island
      UTC_MINUS_1100_BIAS : Int := -660;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_1100_NAME : String := '(UTC -11:00)';   // Time zone name: (UTC -11:00) Midway Island
      UTC_MINUS_1000 : Int := -1000;   // UTC -10:00) Hawaii
      UTC_MINUS_1000_BIAS : Int := -600;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_1000_NAME : String := '(UTC -10:00)';   // Time zone name: UTC -10:00) Hawaii
      UTC_MINUS_0930 : Int := -930;   // (UTC -09:30) (French) Polynesia
      UTC_MINUS_0930_BIAS : Int := -570;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0930_NAME : String := '(UTC -09:30)';   // Time zone name: (UTC -09:30) (French) Polynesia
      UTC_MINUS_0900 : Int := -900;   // (UTC -09:00) Alaska
      UTC_MINUS_0900_BIAS : Int := -540;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0900_NAME : String := '(UTC -09:00)';   // Time zone name: (UTC -09:00) Alaska
      UTC_MINUS_0800 : Int := -800;   // (UTC -08:00) Tijuana, Los Angeles, Seattle, Vancouver
      UTC_MINUS_0800_BIAS : Int := -480;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0800_NAME : String := '(UTC -08:00)';   // Time zone name: (UTC -08:00) Tijuana, Los Angeles, Seattle, Vancouver
      UTC_MINUS_0700 : Int := -700;   // (UTC -07:00) Arizona, Denver, Salt Lake City, Calgary
      UTC_MINUS_0700_BIAS : Int := -420;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0700_NAME : String := '(UTC -07:00)';   // Time zone name: (UTC -07:00) Arizona, Denver, Salt Lake City, Calgary
      UTC_MINUS_0600 : Int := -600;   // (UTC -06:00) Chicago, Dallas, Kansas City, Winnipeg
      UTC_MINUS_0600_BIAS : Int := -360;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0600_NAME : String := '(UTC -06:00)';   // Time zone name: (UTC -06:00) Chicago, Dallas, Kansas City, Winnipeg
      UTC_MINUS_0500 : Int := -500;   // (UTC -05:00) Eastern Time (USA & Canada)
      UTC_MINUS_0500_BIAS : Int := -300;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0500_NAME : String := '(UTC -05:00)';   // Time zone name: (UTC -05:00) Eastern Time (USA & Canada)
      UTC_MINUS_0400 : Int := -400;   // (UTC -04:00) La Paz, Georgetown
      UTC_MINUS_0400_BIAS : Int := -240;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0400_NAME : String := '(UTC -04:00)';   // Time zone name: (UTC -04:00) La Paz, Georgetown
      UTC_MINUS_0330 : Int := -0330;   // (UTC -03:30) Newfoundland
      UTC_MINUS_0330_BIAS : Int := -210;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0330_NAME : String := '(UTC -03:30)';   // Time zone name: (UTC -03:30) Newfoundland
      UTC_MINUS_0300 : Int := -300;   // (UTC -03:00) Brasilia, Buenos Aires
      UTC_MINUS_0300_BIAS : Int := -180;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0300_NAME : String := '(UTC -03:00)';   // Time zone name: (UTC -03:00) Brasilia, Buenos Aires
      UTC_MINUS_0200 : Int := -200;   // (UTC -02:00) Mid-Atlantic
      UTC_MINUS_0200_BIAS : Int := -120;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0200_NAME : String := '(UTC -02:00)';   // Time zone name: (UTC -02:00) Mid-Atlantic
      UTC_MINUS_0100 : Int := -100;   // (UTC -01:00) Azores, Cape Verde Is.
      UTC_MINUS_0100_BIAS : Int := -60;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_MINUS_0100_NAME : String := '(UTC -01:00)';   // Time zone name: (UTC -01:00) Azores, Cape Verde Is.
      UTC_0 : Int := 0;   // (UTC) Dublin, Edinburgh, Lisbon, London
      UTC_0_BIAS : Int := 0;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_0_NAME : String := '(UTC)';   // Time zone name: (UTC) Dublin, Edinburgh, Lisbon, London
      UTC_100 : Int := 100;   // (UTC +01:00) Berlin, Bern, Brussels, Rome, Stockholm, Vienna
      UTC_100_BIAS : Int := 60;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_100_NAME : String := '(UTC +01:00)';   // Time zone name: (UTC +01:00) Berlin, Bern, Brussels, Rome, Stockholm, Vienna
      UTC_200 : Int := 200;   // (UTC +02:00) Athens, Istanbul, Minsk, Bucharest
      UTC_200_BIAS : Int := 120;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_200_NAME : String := '(UTC +02:00)';   // Time zone name: (UTC +02:00) Athens, Istanbul, Minsk, Bucharest
      UTC_300 : Int := 300;   // (UTC +03:00) Moscow, St. Petersburg, Baghdad, Kuwait, Riyadh
      UTC_300_BIAS : Int := 180;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_300_NAME : String := '(UTC +03:00)';   // Time zone name: (UTC +03:00) Moscow, St. Petersburg, Baghdad, Kuwait, Riyadh
      UTC_330 : Int := 330;   // (UTC +03:30) Iran
      UTC_330_BIAS : Int := 210;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_330_NAME : String := '(UTC +03:30)';   // Time zone name: (UTC +03:30) Iran
      UTC_400 : Int := 400;   // (UTC +04:00) Abu Dhabi, Muscat
      UTC_400_BIAS : Int := 240;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_400_NAME : String := '(UTC +04:00)';   // Time zone name: (UTC +04:00) Abu Dhabi, Muscat
      UTC_430 : Int := 430;   // (UTC +04:30) Afghanistan
      UTC_430_BIAS : Int := 270;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_430_NAME : String := '(UTC +04:30)';   // Time zone name: (UTC +04:30) Afghanistan
      UTC_500 : Int := 500;   // (UTC +05:00) Islamabad, Karachi, Tashkent
      UTC_500_BIAS : Int := 300;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_500_NAME : String := '(UTC +05:00)';   // Time zone name: (UTC +05:00) Islamabad, Karachi, Tashkent
      UTC_530 : Int := 530;   // (UTC +05:30) India, Sri Lanka
      UTC_530_BIAS : Int := 330;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_530_NAME : String := '(UTC +05:30)';   // Time zone name: (UTC +05:30) India, Sri Lanka
      UTC_545 : Int := 545;   // (UTC +05:45) Nepal
      UTC_545_BIAS : Int := 345;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_545_NAME : String := '(UTC +05:45)';   // Time zone name: (UTC +05:45) Nepal
      UTC_600 : Int := 600;   // (UTC +06:00) Astana, Almaty, Dhaka, Colombo, Banglatesh, Butan
      UTC_600_BIAS : Int := 360;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_600_NAME : String := '(UTC +06:00)';   // Time zone name: (UTC +06:00) Astana, Almaty, Dhaka, Colombo, Banglatesh, Butan
      UTC_630 : Int := 630;   // (UTC +06:30) Coco Island, Mayanmar
      UTC_630_BIAS : Int := 390;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_630_NAME : String := '(UTC +06:30)';   // Time zone name: (UTC +06:30) Coco Island, Mayanmar
      UTC_700 : Int := 700;   // (UTC +07:00) Bangkok, Hanoi, Jakarta
      UTC_700_BIAS : Int := 420;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_700_NAME : String := '(UTC +07:00)';   // Time zone name: (UTC +07:00) Bangkok, Hanoi, Jakarta
      UTC_800 : Int := 800;   // (UTC +08:00) Beijing, Chongqing, Hong Kong, Urumqi
      UTC_800_BIAS : Int := 480;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_800_NAME : String := '(UTC +08:00)';   // Time zone name: (UTC +08:00) Beijing, Chongqing, Hong Kong, Urumqi
      UTC_830 : Int := 830;   // (UTC +08:30) North Corea (old)
      UTC_830_BIAS : Int := 510;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_830_NAME : String := '(UTC +08:30)';   // Time zone name: (UTC +08:30) North Corea (old)
      UTC_845 : Int := 845;   // (UTC +08:45) Western Australia, Eucla
      UTC_845_BIAS : Int := 525;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_845_NAME : String := '(UTC +08:45)';   // Time zone name: (UTC +08:45) Western Australia, Eucla
      UTC_900 : Int := 900;   // (UTC +09:00) Yakutsk, Osaka, Sapporo, Tokyo, Seoul
      UTC_900_BIAS : Int := 540;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_900_NAME : String := '(UTC +09:00)';   // Time zone name: (UTC +09:00) Yakutsk, Osaka, Sapporo, Tokyo, Seoul
      UTC_930 : Int := 930;   // (UTC +09:30) Australia: Northern Territory, South Australia
      UTC_930_BIAS : Int := 570;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_930_NAME : String := '(UTC +09:30)';   // Time zone name: (UTC +09:30) Australia: Northern Territory, South Australia
      UTC_1000 : Int := 1000;   // (UTC +10:00) Brisbane, Canberra, Melbourne, Sydney
      UTC_1000_BIAS : Int := 600;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1000_NAME : String := '(UTC +10:00)';   // Time zone name: (UTC +10:00) Brisbane, Canberra, Melbourne, Sydney
      UTC_1030 : Int := 1030;   // (UTC +10:30) Australia: Lord Howe Island
      UTC_1030_BIAS : Int := 630;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1030_NAME : String := '(UTC +10:30)';   // Time zone name: (UTC +10:30) Australia: Lord Howe Island
      UTC_1100 : Int := 1100;   // (UTC +11:00) Vladivostok, Magadan, Solomon Is., New Caledonia
      UTC_1100_BIAS : Int := 660;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1100_NAME : String := '(UTC +11:00)';   // Time zone name: (UTC +11:00) Vladivostok, Magadan, Solomon Is., New Caledonia
      UTC_1200 : Int := 1200;   // (UTC +12:00) Auckland, Wellington
      UTC_1200_BIAS : Int := 720;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1200_NAME : String := '(UTC +12:00)';   // Time zone name: (UTC +12:00) Auckland, Wellington
      UTC_1245 : Int := 1245;   // (UTC +12:45) Chatham Islands
      UTC_1245_BIAS : Int := 765;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1245_NAME : String := '(UTC +12:45)';   // Time zone name: (UTC +12:45) Chatham Islands
      UTC_1300 : Int := 1300;   // (UTC +13:00) Tonga, Samoa, Kiribati (Phoenix isl.)
      UTC_1300_BIAS : Int := 780;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1300_NAME : String := '(UTC +13:00)';   // Time zone name: (UTC +13:00) Tonga, Samoa, Kiribati (Phoenix isl.)
      UTC_1400 : Int := 1400;   // (UTC +14:00) Kiribati (Line isl.)
      UTC_1400_BIAS : Int := 840;   // Time difference between the local time and the system time (UTC) in minutes
      UTC_1400_NAME : String := '(UTC +14:00)';   // Time zone name: (UTC +14:00) Kiribati (Line isl.)
      DAYLIGHT_BIAS_ON : Int := 60;   // Daylight saving time changeover active (BIAS = local time + 60 min) 
      DAYLIGHT_BIAS_OFF : Int := 0;   // Daylight saving time changeover inactive (BIAS = 0)
   END_VAR


BEGIN
	REGION BLOCK INFO HEADER
	  //===============================================================================
	  // SIEMENS AG / (c)Copyright 2017
	  //-------------------------------------------------------------------------------
	  // Title:            LGF_SetTime
	  // Comment/Function: Set system / local time, time zone
	  // Library/Family:   LGF(Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      S7-PLCSIM Advanced V2.0 SP1
	  // Engineering:      TIA Portal V14 Update 1
	  // Restrictions:     ENO disabled - error handling done with error and status
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|------------------------------
	  // 01.00.00 08.06.2015  Siemens Industry Online Support
	  //                      First released version
	  // 01.00.01 02.01.2017  Siemens Industry Online Support
	  //                      Upgrade: TIA V14 Update 1
	  // 01.00.02 02.03.2017  Siemens Industry Online Support
	  //                      Bugfix: FB number: automatic
	  // 01.00.03 17.08.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15 Update 2
	  // 01.00.04 23.11.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15.1
	  // 01.00.05 20.02.2019  Siemens Industry Online Support
	  //                      Bugfix: Rising edge at input REQ of SET_TIMEOUT
	  // 01.00.06 23.08.2019  Simatic Systems Support
	  //                      Reworked interface to PLC Open "execute" behavior
	  //                      Magic numbers removed, tag naming added, code reworked
	  // 03.00.00 23.04.2020  Simatic Systems Support
	  //                      Set version to V3.0.0, harmonize the version of the whole library
	  // 03.00.02 13.01.2020  Simatic Systems Support
	  //                      Bug fix - bias correction for time offsets (330)
	  //                      Insert documentation
	  // 03.00.03 03.06.2022  Simatic Systems Support
	  //                      Bug fix - bias correction for time offsets (200)
	  //=============================================================================
	END_REGION BLOCK INFO HEADER
	
	REGION DESCRIPTION
	  (/*
	NOTE
	:    The status of called commands is output in `subFunctionStatus`. In this case, the output value in `status` indicates which command caused the error. In this case, refer to the TIA Portal Online Help section for information on the respective commands.
	
	---
	
	NOTE
	:    The function uses internally the system function `WR_LOC_T` to write the local time of the CPU or `WR_SYS_T` to write the coordinated world time (UTC). Further it uses the system function `SET_TIMEZONE` to set the time zone of the PLC.
	
	---
	This block combines the functions of system time, local time, and set time zone.
	
	------
	
	The following time zones are possible on the `timeZone` input:
	
	+:----------:+:----------------------------------------------------+
	| Input      | Time zone                                           |
	| `timeZone` |                                                     |
	+============+=====================================================+
	| -1200      | (UTC -12:00) Eniwetok, Kwajalein
	+------------+-----------------------------------------------------+
	| -1100      | (UTC -11:00) Midway Island
	+------------+-----------------------------------------------------+
	| -1000      | (UTC -10:00) Hawaii
	+------------+-----------------------------------------------------+
	| -930       | (UTC -09:30) (French) Polynesia
	+------------+-----------------------------------------------------+
	| -900       | (UTC -09:00) Alaska
	+------------+-----------------------------------------------------+
	| -800       | (UTC -08:00) Tijuana, Los Angeles, Seattle, Vancouver
	+------------+-----------------------------------------------------+
	| -700       | (UTC -07:00) Arizona, Denver, Salt Lake City, Calgary
	+------------+-----------------------------------------------------+
	| -600       | (UTC -06:00) Chicago, Dallas, Kansas City, Winnipeg
	+------------+-----------------------------------------------------+
	| -500       | (UTC -05:00) Eastern Time (USA & Canada)
	+------------+-----------------------------------------------------+
	| -400       | (UTC -04:00) La Paz, Georgetown
	+------------+-----------------------------------------------------+
	| -330       | (UTC -03:30) Newfoundland
	+------------+-----------------------------------------------------+
	| -300       | (UTC -03:00) Brasilia, Buenos Aires
	+------------+-----------------------------------------------------+
	| -200       | (UTC -02:00) Mid-Atlantic
	+------------+-----------------------------------------------------+
	| -100       | (UTC -01:00) Azores, Cape Verde Is.
	+------------+-----------------------------------------------------+
	| 0          | (UTC) Dublin, Edinburgh, Lisbon, London
	+------------+-----------------------------------------------------+
	| 100        | (UTC +01:00) Berlin, Bern, Brussels, Rome, Stockholm, Vienna
	+------------+-----------------------------------------------------+
	| 200        | (UTC +02:00) Athens, Istanbul, Minsk, Bucharest
	+------------+-----------------------------------------------------+
	| 300        | (UTC +03:00) Moscow, St. Petersburg, Baghdad, Kuwait, Riyadh
	+------------+-----------------------------------------------------+
	| 330        | (UTC +03:30) Iran: Tehran
	+------------+-----------------------------------------------------+
	| 400        | (UTC +04:00) Abu Dhabi, Muscat
	+------------+-----------------------------------------------------+
	| 430        | (UTC +04:30) Afghanistan: Kabul
	+------------+-----------------------------------------------------+
	| 500        | (UTC +05:00) Islamabad, Karachi, Tashkent
	+------------+-----------------------------------------------------+
	| 530        | (UTC +05:30) India, Sri Lanka
	+------------+-----------------------------------------------------+
	| 545        | (UTC +05:45) Nepal
	+------------+-----------------------------------------------------+
	| 600        | (UTC +06:00) Astana, Almaty, Dhaka, Colombo
	+------------+-----------------------------------------------------+
	| 630        | (UTC +06:30) Coco Island, Myanmar
	+------------+-----------------------------------------------------+
	| 700        | (UTC +07:00) Bangkok, Hanoi, Jakarta
	+------------+-----------------------------------------------------+
	| 800        | (UTC +08:00) Beijing, Chongqing, Hong Kong, Urumqi
	+------------+-----------------------------------------------------+
	| 830        | (UTC +08:30) North Korea old
	+------------+-----------------------------------------------------+
	| 845        | (UTC +08:45) Western Australia: Eucla
	+------------+-----------------------------------------------------+
	| 900        | (UTC +09:00) Yakutsk, Osaka, Sapporo, Tokyo, Seoul
	+------------+-----------------------------------------------------+
	| 930        | (UTC +09:30) Australia: Northern Territory, South Australia
	+------------+-----------------------------------------------------+
	| 1000       | (UTC +10:00) Brisbane, Canberra, Melbourne, Sydney
	+------------+-----------------------------------------------------+
	| 1030       | (UTC +10:30) Australia: Lord Howe Island
	+------------+-----------------------------------------------------+
	| 1100       | (UTC +11:00) Vladivostok, Magadan, Solomon Is., New Caledonia
	+------------+-----------------------------------------------------+
	| 1200       | (UTC +12:00) Auckland, Wellington
	+------------+-----------------------------------------------------+
	| 1245       | (UTC +12:45) Chatham Islands
	+------------+-----------------------------------------------------+
	| 1300       | (UTC +13:00) Tonga, Samoa
	+------------+-----------------------------------------------------+
	| 1400       | (UTC +14:00) Kiribati
	+------------+-----------------------------------------------------+
	
	Note
	:    Daylight saving time/standard time
	:    The parameters (time difference, start summer time, start winter time) must be
	adapted to the desired time zone in the static variable `statTimeZone`.
	
	##### Adjusting parameters in the `statTimeZone` variable
	
	The static variable `statTimeZone` in the block interface is of the system data type TimeTransformationRule. In this system data type, the parameters for the local time zone and the summer/winter time changeover are stored.
	
	The default values of the static variable `statTimeZone` are set to Central European Summer Time in the block interface:
	
	* Time difference: 60 min
	* Start summer time: last Sunday in March, 02:00 a.m.
	* Start winter time: last Sunday in October, 03:00 a.m.
	
	The following Figure shows the settings for the summer/winter time changeover of
	Central European Summer Time.
	
	The parameter `Bias` is determined by the input parameter `timeZone`. The parameter `DaylightBias` depends on the input parameter `daylightSavingTime` and is either `0` or `60`.
	
	For other time zones, the parameters for summer/winter time changeover must be
	adjusted (marked below).  
	
	![LGF_SetTime - Adjusting parameters in the 'statTimeZone' variable](LGF_SetTime.PNG "LGF_SetTime - Adjusting parameters in the 'statTimeZone' variable")  
	
	*/)
	END_REGION DESCRIPTION
	
	#tempExecute := #execute; // Work with temporary value / create process image
	
	REGION TRIGGERING
	  IF (#tempExecute = TRUE) AND (#statExecuteOld = FALSE) // Check if FB is triggered
	    // FB shall finish current job before new job can be started with rising edge of execute
	    AND (#statStatus = #STATUS_NO_CALL)
	  THEN // First call; initialize FB
	    #statDone := FALSE;
	    #statBusy := TRUE;
	    #statError := FALSE;
	    #statStatus := #STATUS_FIRST_CALL;
	    #statSubFunctionStatus := #STATUS_EXECUTION_FINISHED_NO_ERROR;
	    // State machine - start processing
	    #statFBState := #FB_STATE_SET_TIME;
	    
	    // Initialize functionality: reset of variables, diagnostics, etc.
	    // Set timzone values for systemfunction
	    CASE #timeZone OF
	      #UTC_MINUS_1200:  // (UTC -12:00) Eniwetok, Kwajalein
	        #statTimeZone.Bias := #UTC_MINUS_1200_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_1200_NAME;
	      #UTC_MINUS_1100:  // (UTC -11:00) Midway Island
	        #statTimeZone.Bias := #UTC_MINUS_1100_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_1100_NAME;
	      #UTC_MINUS_1000:  // (UTC -10:00) Hawaii
	        #statTimeZone.Bias := #UTC_MINUS_1000_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_1000_NAME;
	      #UTC_MINUS_0930:  // (UTC -09:30) (French) Polynesia
	        #statTimeZone.Bias := #UTC_MINUS_0930_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0930_NAME;
	      #UTC_MINUS_0900:  // (UTC -09:00) Alaska
	        #statTimeZone.Bias := #UTC_MINUS_0900_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0900_NAME;
	      #UTC_MINUS_0800:  // (UTC -08:00) Tijuana, Los Angeles, Seattle, Vancouver
	        #statTimeZone.Bias := #UTC_MINUS_0800_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0800_NAME;
	      #UTC_MINUS_0700:  // (UTC -07:00) Arizona, Denver, Salt Lake City, Calgary
	        #statTimeZone.Bias := #UTC_MINUS_0700_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0700_NAME;
	      #UTC_MINUS_0600:  // (UTC -06:00) Chicago, Dallas, Kansas City, Winnipeg
	        #statTimeZone.Bias := #UTC_MINUS_0600_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0600_NAME;
	      #UTC_MINUS_0500:  // (UTC -05:00) Eastern Time (USA & Canada)
	        #statTimeZone.Bias := #UTC_MINUS_0500_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0500_NAME;
	      #UTC_MINUS_0400:  // (UTC -04:00) La Paz, Georgetown
	        #statTimeZone.Bias := #UTC_MINUS_0400_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0400_NAME;
	      #UTC_MINUS_0330:  // (UTC -03:30) Newfoundland
	        #statTimeZone.Bias := #UTC_MINUS_0330_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0330_NAME;
	      #UTC_MINUS_0300:  // (UTC -03:00) Brasilia, Buenos Aires
	        #statTimeZone.Bias := #UTC_MINUS_0300_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0300_NAME;
	      #UTC_MINUS_0200:  // (UTC -02:00) Mid-Atlantic
	        #statTimeZone.Bias := #UTC_MINUS_0200_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0200_NAME;
	      #UTC_MINUS_0100:  // (UTC -01:00) Azores, Cape Verde Is.
	        #statTimeZone.Bias := #UTC_MINUS_0100_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_MINUS_0100_NAME;
	      #UTC_0:  // (UTC) Dublin, Edinburgh, Lisbon, London
	        #statTimeZone.Bias := #UTC_0_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_0_NAME;
	      #UTC_100:  // (UTC +01:00) Berlin, Bern, Brussels, Rome, Stockholm, Vienna
	        #statTimeZone.Bias := #UTC_100_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_100_NAME;
	      #UTC_200: // (UTC +02:00) Athens, Istanbul, Minsk, Bucharest
	        #statTimeZone.Bias := #UTC_200_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_200_NAME;
	      #UTC_300: // (UTC +03:00) Moscow, St. Petersburg, Baghdad, Kuwait, Riyadh
	        #statTimeZone.Bias := #UTC_300_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_300_NAME;
	      #UTC_330: // (UTC +03:00) Iran
	        #statTimeZone.Bias := #UTC_330_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_330_NAME;
	      #UTC_400: // (UTC +04:00) Abu Dhabi, Muscat
	        #statTimeZone.Bias := #UTC_400_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_400_NAME;
	      #UTC_430: // (UTC +04:30) Afghanistan
	        #statTimeZone.Bias := #UTC_430_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_430_NAME;
	      #UTC_500: // (UTC +05:00) Islamabad, Karachi, Tashkent
	        #statTimeZone.Bias := #UTC_500_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_500_NAME;
	      #UTC_530: // (UTC +05:30) India, Sri Lanka
	        #statTimeZone.Bias := #UTC_530_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_530_NAME;
	      #UTC_545: // (UTC +05:45) Nepal
	        #statTimeZone.Bias := #UTC_545_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_545_NAME;
	      #UTC_600: // (UTC +06:00) Astana, Almaty, Dhaka, Colombo, Banglatesh, Butan
	        #statTimeZone.Bias := #UTC_600_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_600_NAME;
	      #UTC_630: // (UTC +06:30) Coco Island, Mayanmar
	        #statTimeZone.Bias := #UTC_630_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_630_NAME;
	      #UTC_700: // (UTC +07:00) Bangkok, Hanoi, Jakarta
	        #statTimeZone.Bias := #UTC_700_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_700_NAME;
	      #UTC_800: // (UTC +08:00) Beijing, Chongqing, Hong Kong, Urumqi
	        #statTimeZone.Bias := #UTC_800_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_800_NAME;
	      #UTC_830: // (UTC +08:30) North Corea (old)
	        #statTimeZone.Bias := #UTC_830_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_830_NAME;
	      #UTC_845: // (UTC +08:45) Western Australia, Eucla
	        #statTimeZone.Bias := #UTC_845_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_845_NAME;
	      #UTC_900: // (UTC +09:00) Yakutsk, Osaka, Sapporo, Tokyo, Seoul
	        #statTimeZone.Bias := #UTC_900_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_900_NAME;
	      #UTC_930: // (UTC +09:30) Australia: Northern Territory, South Australia
	        #statTimeZone.Bias := #UTC_930_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_930_NAME;
	      #UTC_1000: // (UTC +10:00) Brisbane, Canberra, Melbourne, Sydney
	        #statTimeZone.Bias := #UTC_1000_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1000_NAME;
	      #UTC_1030: // (UTC +10:30) Australia: Lord Howe Island
	        #statTimeZone.Bias := #UTC_1030_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1030_NAME;
	      #UTC_1100: // (UTC +11:00) Vladivostok, Magadan, Solomon Is., New Caledonia
	        #statTimeZone.Bias := #UTC_1100_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1100_NAME;
	      #UTC_1200: // (UTC +12:00) Auckland, Wellington
	        #statTimeZone.Bias := #UTC_1200_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1200_NAME;
	      #UTC_1245: // (UTC +12:45) Chatham Islands
	        #statTimeZone.Bias := #UTC_1245_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1245_NAME;
	      #UTC_1300: // (UTC +13:00) Tonga, Samoa, Kiribati (Phoenix isl.)
	        #statTimeZone.Bias := #UTC_1300_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1300_NAME;
	      #UTC_1400: // (UTC +13:00) Kiribati (Line isl.)
	        #statTimeZone.Bias := #UTC_1400_BIAS;
	        #statTimeZone.TimeZoneName := #UTC_1400_NAME;
	      ELSE  // Error handling - wronge time zone parmater
	        #statStatus := #ERR_WRONG_TIMEZONE;
	        #subFunctionStatus := INT_TO_WORD(#timeZone);
	        #statFBState := #FB_STATE_NO_PROCESSING;
	    END_CASE;
	    
	    // Select daylight saving time on/off
	    IF #isDaylightSavingTime THEN
	      #statTimeZone.DaylightBias := #DAYLIGHT_BIAS_ON;
	    ELSE
	      #statTimeZone.DaylightBias := #DAYLIGHT_BIAS_OFF;
	    END_IF;
	    
	    // Initialize functionality: call subsidiary FBs with FALSE
	    #instSetTimeZone(REQ      := FALSE,
	                     TimeZone := #statTimeZone,
	                     DONE     => #tempSetTimeZoneDone,
	                     BUSY     => #tempSetTimeZoneBusy,
	                     ERROR    => #tempSetTimeZoneError,
	                     STATUS   => #tempSetTimeZoneStatus);
	    
	  ELSIF (#statStatus = #STATUS_FIRST_CALL) THEN
	    #statStatus := #STATUS_SUBSEQUENT_CALL;
	  END_IF;
	  
	  // Edge detection 'execute' input
	  #statExecuteOld := #tempExecute;
	END_REGION TRIGGERING
	
	IF (#statStatus = #STATUS_NO_CALL) THEN // Nothing to do -> End here to reduce "system load"
	  RETURN;
	END_IF;
	
	REGION STATE_MACHINE
	  CASE #statFBState OF // State machine of FB
	    #FB_STATE_NO_PROCESSING: // No processing active (Note: this state must always be present and left empty)
	      REGION No processing
	        ;
	      END_REGION
	      
	    #FB_STATE_SET_TIME:
	      REGION Set time
	        IF #isLocalTime THEN
	          #tempResultSetTime := INT_TO_WORD(WR_LOC_T(LOCTIME := #systemTime, DST := #isDaylightSavingTime));
	          
	          // In case of error - set error output an quit
	          IF #tempResultSetTime.%X15 = TRUE THEN
	            #statStatus := #ERR_SET_TIME_LOCAL;
	            #statSubFunctionStatus := #tempResultSetTime;
	          ELSE
	            #statFBState := #FB_STATE_SET_TIMEZONE;
	          END_IF;
	          
	        ELSE
	          #tempResultSetTime := INT_TO_WORD(WR_SYS_T(#systemTime));
	          
	          // In case of error - set error output an quit
	          IF #tempResultSetTime.%X15 = TRUE THEN
	            #statStatus := #ERR_SET_TIME_UTC;
	            #statSubFunctionStatus := #tempResultSetTime;
	          ELSE
	            #statFBState := #FB_STATE_SET_TIMEZONE;
	          END_IF;
	        END_IF;
	      END_REGION
	      
	    #FB_STATE_SET_TIMEZONE:
	      REGION Set timezone
	        // Set time zone by calling the system function
	        #instSetTimeZone(REQ      := TRUE,
	                         TimeZone := #statTimeZone,
	                         DONE     => #tempSetTimeZoneDone,
	                         BUSY     => #tempSetTimeZoneBusy,
	                         ERROR    => #tempSetTimeZoneError,
	                         STATUS   => #tempSetTimeZoneStatus);
	        
	        // If operation is done
	        IF #instSetTimeZone.DONE = TRUE THEN
	          // Reset function call
	          #instSetTimeZone(REQ      := FALSE,
	                           TimeZone := #statTimeZone,
	                           DONE     => #tempSetTimeZoneDone,
	                           BUSY     => #tempSetTimeZoneBusy,
	                           ERROR    => #tempSetTimeZoneError,
	                           STATUS   => #tempSetTimeZoneStatus);
	          // Next state
	          #statStatus := #STATUS_EXECUTION_FINISHED_NO_ERROR;
	        END_IF;
	        
	        // If operation is done
	        IF #instSetTimeZone.ERROR = TRUE THEN
	          // Set error message
	          #statStatus := #ERR_SET_TIMEZONE;
	          #statSubFunctionStatus := #instSetTimeZone.STATUS;
	          
	          // Reset function call
	          #instSetTimeZone(REQ      := FALSE,
	                           TimeZone := #statTimeZone,
	                           DONE     => #tempSetTimeZoneDone,
	                           BUSY     => #tempSetTimeZoneBusy,
	                           ERROR    => #tempSetTimeZoneError,
	                           STATUS   => #tempSetTimeZoneStatus);
	        END_IF;
	      END_REGION
	      
	    ELSE // Undefined state in state machine reached
	      #statStatus := #ERR_UNDEFINED_STATE;
	  END_CASE;
	END_REGION STATE_MACHINE
	
	REGION OUTPUTS
	  // Write outputs
	  IF (#statStatus = #STATUS_EXECUTION_FINISHED_NO_ERROR) AND (#statDone = FALSE) THEN // Execution finished without errors
	    #statDone := TRUE;
	    #statBusy := FALSE;
	    #statError := FALSE;
	    // Execution aborted --> set state no processing
	    #statFBState := #FB_STATE_NO_PROCESSING;
	    
	  ELSIF (#statStatus.%X15 = TRUE) AND (#statError = FALSE) THEN // Error occurred (#statStatus is 16#8000 to 16#FFFF)
	    #statDone := FALSE;
	    #statBusy := FALSE;
	    #statError := TRUE;
	    // Execution aborted --> set state no processing
	    #statFBState := #FB_STATE_NO_PROCESSING;
	    
	  ELSIF (#tempExecute = FALSE) AND ((#statDone = TRUE) OR (#statError = TRUE)) THEN // Reset outputs
	    #statDone := FALSE;
	    #statBusy := FALSE;
	    #statError := FALSE;
	    #statStatus := #STATUS_NO_CALL;
	  END_IF;
	  
	  // Write static values to outputs
	  #done := #statDone;
	  #busy := #statBusy;
	  #error := #statError;
	  #status := #statStatus;
	  #subFunctionStatus := #statSubFunctionStatus;
	  // Output the last set time zone
	  #lastSetTimeZone := #statTimeZone.TimeZoneName;
	  
	  // ENO is not used, forced to TRUE
	  ENO := TRUE;
	END_REGION OUTPUTS
	
END_FUNCTION_BLOCK

