FUNCTION_BLOCK "LGF_DifferenceQuotientFB"
TITLE = LGF_DifferenceQuotientFB
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Digital_Industry
FAMILY : LGF
NAME : LGF_DifferenceQuotientFB
//This function numerically differentiates a signal sampled equidistantly in time. For example, the velocity can be calculated from a measured locus curve, or the acceleration can be calculated from the measured velocity. In order to minimize the effects of a scattering measurement signal, this algorithm uses a compensating polynomial.
//
//The function block calculates the differentiated values cyclically.
//
//The function block reads-in a value with each positive edge on the `insert` been read in, the block calculates a differentiated value and outputs it.
   VAR_INPUT 
      enable : Bool;   // Activates the block. As long as enable is `TRUE`, the block can accept values on the parameter `value`.
      insert : Bool;   // Accepts the value at the input `value` at positive edge and outputs a `derivatedValue` if five values have been read in.
      value : LReal;   // Value that must be included in the differentiation.
      deltaT : LReal;   // Equidistant distance between two measured values. (e.g. 1s)
   END_VAR

   VAR_OUTPUT 
      derivatedValue { ExternalWritable := 'False'} : LReal;   // The differentiated value.
      error { ExternalWritable := 'False'} : Bool;   // FALSE: No error  TRUE: An error occurred during the execution of the FB
      status { ExternalWritable := 'False'} : Word;   // 16#0000-16#7FFF: Status of the FB  16#8000-16#FFFF: Error identification (see following Table)
   END_VAR

   VAR 
      statValues { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Array[0..#MINIMUM_NUMBER_OF_VALUES] of LReal;   // Static array values
      statCount { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;   // Static value for count
      statDerivatedValue { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : LReal;   // Static variable of  derivated value
      statStatus { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Word;   // Static variable of status
      statEnableOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static old value of enable for edge detection
      statInsertOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static old value for insert for edge detection
   END_VAR

   VAR CONSTANT 
      ZERO : LReal := 0.0;   // Zero constant for reinitializing and validation checks
      MINIMUM_NUMBER_OF_VALUES : UDInt := 4;   // The needed minimum number of inserted values
      INCREMENT_VALUE : Int := 1;   // Increment value
      STATUS_NO_CALL : Word := 16#7000;   // Status: No call of FB.  The block waits for activation through the parameter `enable`.
      STATUS_FIRST_CALL : Word := 16#7001;   // Status: First call of FB after enabling
      STATUS_SUBSEQUENT_CALL : Word := 16#7002;   // Status: Processing is active.  Subsequent call of FB.
      STATUS_NOT_ENOUGH_VALUES : Word := 16#7010;   // Status: Not enough values. The block requires five (5) values to calculate a differentiated value. Transfer additional values with a positive edge on the `insert` input.
      ERR_DELTA_T : Word := 16#8200;   // Error: Delta time `deltaT` must not be zero.
   END_VAR


BEGIN
	REGION Block info header
	  //============================================================================
	  // SIEMENS AG / (c)Copyright 2018
	  //----------------------------------------------------------------------------
	  // Title:            LGF_DifferenceQuotientFB
	  // Comment/Function: This function block differentiates a signal
	  //                   Magic numbers that are part of mathematics formula are
	  //                   left without constants.
	  // Library/Family:   LGF(Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      S7-PLCSIM Advanced V2.0 SP1
	  // Engineering:      TIA Portal V15 Update 2
	  // Restrictions:     ENO disabled, forced to true - error handling done by error and status
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //----------|------------|------------------------|----------------------------
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|----------------------------
	  // 01.00.00 | 23.11.2018 | Siemens Industry Online Support
	  //                       | First released version
	  // 01.00.01 | 04.11.2019 | Simatic Systems Support
	  //                       | Code reworked.
	  //                       | Regions, comments and constants are added
	  // 03.00.00 | 23.04.2020 | Simatic Systems Support
	  //                       | Set version to V3.0.0, harmonize the version of the whole library
	  // 03.00.01 | 06.04.2021 | Simatic Systems Support
	  //                       | Insert documentation
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/*
	To calculate the difference quotient of a scattering signal, a third-degree compensation polynomial is first placed through the measured values. This polynomial is then differentiated. With this method, even a distorted input signal can be sensibly differentiated.
	
	The difference quotient is calculated with the following formula:
	
	$$
	   y^{\prime}(n) = \frac{y(n-2) - 8y(n-1) + 8y(n+1) - y(n+2)}{12 \cdot deltaT}
	$$
	
	
	$deltaT$: equidistant distance between two measured values (e.g. 1s).
	
	The function (FC) can calculate $N-4$ differentiated and smoothed measured values from N measured values. The output array would be assigned with 0 in the index (0,1,N-1,N). However, the following formalisms can be used to calculate substitute values:
	
	$$
	   y^{\prime}(n-2) = \frac{-125(y(n-2) + 136y(n-1) + 48y(n) - 88y(n+1) + 29y(n+2)}{84 \cdot deltaT}
	$$
	
	$$
	   y^{\prime}(n-1) = \frac{-38(y(n-2) - 2y(n-1) + 24y(n) + 26y(n+1) - 10y(n+2)}{84 \cdot deltaT}
	$$
	
	$$
	   y^{\prime}(n+1) = \frac{10(y(n-2) - 26y(n-1) - 24y(n) + 2y(n+1) + 38y(n+2)}{84 \cdot deltaT}
	$$
	
	$$
	   y^{\prime}(n+2) = \frac{-29(y(n-2) + 88y(n-1) - 48y(n) - 136y(n+1) + 125y(n+2)}{84 \cdot deltaT}
	$$
	*/)
	END_REGION DESCRIPTION
	
	REGION Check function block call 
	  // // Check if FB is triggered - rising edge on enable
	  IF #enable AND NOT #statEnableOld THEN
	    // Reset 
	    #statCount := 0;
	    #statValues[0] := #ZERO;
	    #statValues[1] := #ZERO;
	    #statValues[2] := #ZERO;
	    #statValues[3] := #ZERO;
	    #statValues[4] := #ZERO;
	    
	    // Set status for first call
	    #statStatus := #STATUS_FIRST_CALL;
	    
	    // enabled after edge trigger
	  ELSIF #enable AND #statEnableOld THEN
	    REGION Values inserting and smoothing calculation
	      // positive edge of insert detected 
	      IF #insert AND NOT #statInsertOld THEN
	        // copy new value
	        #statValues[0] := #statValues[1]; // -> n-2
	        #statValues[1] := #statValues[2]; // -> n-1
	        #statValues[2] := #statValues[3]; // -> n
	        #statValues[3] := #statValues[4]; // -> n+1
	        #statValues[4] := #value; // -> n+2
	        
	        // Check if the minimum needed insered values is reached
	        // Increase the count of added values
	        IF #statCount <= #MINIMUM_NUMBER_OF_VALUES THEN
	          #statCount += #INCREMENT_VALUE;
	          #statStatus := #STATUS_NOT_ENOUGH_VALUES;
	          
	          // DeltaT cannot be zero, devision by zero not possible.
	        ELSIF #deltaT = #ZERO THEN
	          #statStatus := #ERR_DELTA_T;
	        ELSE
	          // calculate derivate value 
	          // keep the "Magic Numbers" as they are part of the Math equation and would extent the formular in a unreadable way
	          // ẏ(n)=1/12deltaT*(y(n-2)-8y(n-1)+8y(n+1)-y(n+2))
	          #statDerivatedValue := (1.0 / (12.0 * #deltaT)) * (#statValues[0] - 8 * #statValues[1] + 8 * #statValues[3] - #statValues[4]);
	          #statStatus := #STATUS_SUBSEQUENT_CALL;
	        END_IF;
	      END_IF;
	    END_REGION
	  ELSE
	    #statDerivatedValue := #ZERO;
	    #statStatus := #STATUS_NO_CALL;
	  END_IF;
	  
	  // store previous values for edge dedection
	  #statEnableOld := #enable;
	  #statInsertOld := #insert;
	END_REGION
	
	REGION Writting to outputs
	  #error := #statStatus.%X15;
	  #status := #statStatus;
	  #derivatedValue := #statDerivatedValue;
	  
	  // ENO mechanism is not used
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION_BLOCK

