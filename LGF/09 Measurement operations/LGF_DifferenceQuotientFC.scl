FUNCTION "LGF_DifferenceQuotientFC" : Void
TITLE = LGF_DifferenceQuotientFC
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Digital_Industry
FAMILY : LGF
NAME : LGF_DifferenceQuotientFC
//This function numerically differentiates a signal sampled equidistantly in time. For example, the velocity can be calculated from a measured locus curve, or the acceleration can be calculated from the measured velocity. In order to minimize the effects of a scattering measurement signal, this algorithm uses a compensating polynomial.
//
//The function calculates the differentiated values acyclically.
//
//The function reads an array that is differentiated. $N-4$ smoothed measured values can be calculated from N measured values. The output array contains the value 0 in the index (0,1,N-1,N). However, replacement values can be calculated.
   VAR_INPUT 
      deltaT : LReal;   // Equidistant distance between two measured values. (e.g. 1s)
   END_VAR

   VAR_OUTPUT 
      error : Bool;   // FALSE: No error  TRUE: An error occurred during the execution of the FB
      status : Word;   // 16#0000-16#7FFF: Status of the FB  16#8000-16#FFFF: Error identification (see following Table)
   END_VAR

   VAR_IN_OUT 
      values : Array[*] of LReal;   // Values that will be included in the differentiation.
      derivatedValues : Array[*] of LReal;   // The differentiated value range.
   END_VAR

   VAR_TEMP 
      tempLowerBound : Int;   // Temp lower bound of input array
      tempUpperBound : Int;   // Temp upper bound of input array
      tempNumberOfElements : Int;   // Temp number of elements of input array
      tempLowerBoundOut : Int;   // Temp lower bound of output array
      tempUpperBoundOut : Int;   // Temp upper bound of output array
      tempNumberOfElementsOut : Int;   // Temp number of elements of output array
      tempCount : Int;   // Temp loop index
      tempDivision : LReal;   // Temp intermediate calulcation variable
   END_VAR

   VAR CONSTANT 
      ZERO : LReal := 0.0;   // Zero constant for reinitializing and validation checks
      NUMBER_OF_ELEMENTS_CORRECTION : Int := 1;   // Count also the starting value. e.g. [0.. 9] = 10 elements
      DIMENSION_ONE : UDInt := 1;   // First dimension of an array
      MINIMUM_ARRAY_ELEMENTS : UDInt := 4;   // The needed minimum number of ARRAY elements. Starting from 0
      STATUS_FINISHED_NO_ERROR : Word := 16#0000;   // Status: Execution finished without errors
      ERR_DELTA_T : Word := 16#8200;   // Error: Delta time `deltaT` must not be zero.
      ERR_ARRAYS_DIFFERENT : Word := 16#8400;   // Error: The Array sizes are not equal.  The arrays `values` and `derivatedValues` must have the same size.
      ERR_NOT_ENOUGH_VALUES : Word := 16#8401;   // Error: Not enough values. The block requires five (5) values to calculate a differntiated value. Transfer additional values with a positive edge on the `insert` input.
   END_VAR


BEGIN
	REGION Block info header
	  //============================================================================
	  // SIEMENS AG / (c)Copyright 2017
	  //----------------------------------------------------------------------------
	  // Title:            LGF_DifferenceQuotientFC
	  // Comment/Function: This function differentiates a signal
	  // Library/Family:   LGF(Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      S7-PLCSIM Advanced V2.0 SP1
	  // Engineering:      TIA Portal V15 Update 2
	  // Restrictions:     ENO disabled - error handling done by error and status
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //----------|------------|------------------------|----------------------------
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|----------------------------
	  // 01.00.00 | 23.11.2018 | Siemens Industry Online Support
	  //                       | First released version
	  // 01.00.01 | 15.11.2019 | Simatic Systems Support
	  //                       | Regions, comments and constants are added
	  // 03.00.00 | 23.04.2020 | Simatic Systems Support
	  //                       | Set version to V3.0.0, harmonize the version of the whole library
	  // 03.00.01 | 06.04.2021 | Simatic Systems Support
	  //                       | Insert documentation
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/*
	To calculate the difference quotient of a scattering signal, a third-degree compensation polynomial is first placed through the measured values. This polynomial is then differentiated. With this method, even a distorted input signal can be sensibly differentiated.
	
	The difference quotient is calculated with the following formula:
	
	$$
	   y^{\prime}(n) = \frac{y(n-2) - 8y(n-1) + 8y(n+1) - y(n+2)}{12 \cdot deltaT}
	$$
	
	
	$deltaT$: equidistant distance between two measured values (e.g. 1s).
	
	The function (FC) can calculate $N-4$ differentiated and smoothed measured values from N measured values. The output array would be assigned with 0 in the index (0,1,N-1,N). However, the following formalisms can be used to calculate substitute values:
	
	$$
	   y^{\prime}(n-2) = \frac{-125(y(n-2) + 136y(n-1) + 48y(n) - 88y(n+1) + 29y(n+2)}{84 \cdot deltaT}
	$$
	
	$$
	   y^{\prime}(n-1) = \frac{-38(y(n-2) - 2y(n-1) + 24y(n) + 26y(n+1) - 10y(n+2)}{84 \cdot deltaT}
	$$
	
	$$
	   y^{\prime}(n+1) = \frac{10(y(n-2) - 26y(n-1) - 24y(n) + 2y(n+1) + 38y(n+2)}{84 \cdot deltaT}
	$$
	
	$$
	   y^{\prime}(n+2) = \frac{-29(y(n-2) + 88y(n-1) - 48y(n) - 136y(n+1) + 125y(n+2)}{84 \cdot deltaT}
	$$
	*/)
	END_REGION DESCRIPTION
	
	REGION Checking number of lower and upper bound  
	  #tempLowerBound := DINT_TO_INT(LOWER_BOUND(ARR := #values, DIM := #DIMENSION_ONE));
	  #tempUpperBound := DINT_TO_INT(UPPER_BOUND(ARR := #values, DIM := #DIMENSION_ONE));
	  
	  #tempNumberOfElements := #tempUpperBound - #tempLowerBound + #NUMBER_OF_ELEMENTS_CORRECTION;
	  
	  #tempLowerBoundOut := DINT_TO_INT(LOWER_BOUND(ARR := #derivatedValues, DIM := #DIMENSION_ONE));
	  #tempUpperBoundOut := DINT_TO_INT(UPPER_BOUND(ARR := #derivatedValues, DIM := #DIMENSION_ONE));
	  
	  #tempNumberOfElementsOut := #tempUpperBoundOut - #tempLowerBoundOut + #NUMBER_OF_ELEMENTS_CORRECTION;
	  
	  //check if size of arrays is ok
	  IF #tempNumberOfElementsOut <> #tempNumberOfElements THEN
	    #error := true;
	    #status := #ERR_ARRAYS_DIFFERENT;
	    RETURN;
	    // check size of arrays if the are big enough
	  ELSIF #tempNumberOfElements <= #MINIMUM_ARRAY_ELEMENTS THEN
	    #error := true;
	    #status := #ERR_NOT_ENOUGH_VALUES;
	    RETURN;
	    // DeltaT cannot be zero, devision by zero not possible.
	  ELSIF #deltaT = #ZERO THEN
	    #error := true;
	    #status := #ERR_DELTA_T;
	    RETURN;
	  END_IF;
	END_REGION
	
	REGION Calculating the derrivate values  
	  // calculate derivate value 
	  // keep the "Magic Numbers" as they are part of the Math equation and would extent the formular in a unreadable way
	  // ẏ(n)=1/12h*(y(n-2)-8y(n-1)+8y(n+1)-y(n+2))
	  // h = deltaT
	  #tempDivision := 1.0 / (12.0 * #deltaT);
	  FOR #tempCount := #tempLowerBound + 2 TO #tempUpperBound - 2 DO
	    #derivatedValues[#tempCount] := #tempDivision * (#values[#tempCount - 2] - 8 * #values[#tempCount - 1] + 8 * #values[#tempCount + 1] - #values[#tempCount + 2]);
	  END_FOR;
	  
	  // ẏ(n-2)=1/84h*(-125y(n-2)+136y(n-1)+48y(n)-88y(n+1)+29y(n+2))
	  // h = deltaT
	  #tempDivision := 1.0 / (84.0 * #deltaT);
	  #derivatedValues[0] := #tempDivision * (-125 * #values[0] + 136 * #values[1] + 48 * #values[2] - 88 * #values[3] + 29 * #values[4]);
	  
	  // ẏ(n-1)=1/84h*(-38y(n-2)-2y(n-1)+24y(n)+26y(n+1)-10y(n+2))
	  // h = deltaT
	  #derivatedValues[1] := #tempDivision * (-38 * #values[0] - 2 * #values[1] + 24 * #values[2] + 26 * #values[3] - 10 * #values[4]);
	  
	  // ẏ(n+1)=1/84h(10y(n-2)-26y(n-1)-24y(n)+2y(n+1)+38y(n+2))
	  // h = deltaT
	  #derivatedValues[#tempUpperBound - 1] := #tempDivision * (10 * #values[#tempUpperBound - 4] - 26 * #values[#tempUpperBound - 3] - 24 * #values[#tempUpperBound - 2] + 2 * #values[#tempUpperBound - 1] + 38 * #values[#tempUpperBound]);
	  
	  // y(n-2)=1/84h(-29y(n-2)+88y(n-1)-48y(n)-136y(n+1)+125y(n+2))
	  // h = deltaT
	  #derivatedValues[#tempUpperBound] := #tempDivision * (-29 * #values[#tempUpperBound - 4] + 88 * #values[#tempUpperBound - 3] - 48 * #values[#tempUpperBound - 2] - 136 * #values[#tempUpperBound - 1] + 125 * #values[#tempUpperBound]);
	  
	  #error := false;
	  #status := #STATUS_FINISHED_NO_ERROR;
	  // ENO mechanism is not used - forced to true.
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION

