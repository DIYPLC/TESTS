FUNCTION_BLOCK "LGF_Impulse"
TITLE = LGF_Impulse
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Industry_Support
FAMILY : LGF
NAME : LGF_Impulse
//This function generates pulses at a given frequency. The pulse is always present for one (control) cycle.
   VAR_INPUT 
      frequency : Real;   // Clock frequency in Hz
   END_VAR

   VAR_OUTPUT 
      impulse { ExternalWritable := 'False'} : Bool;   // Impulse signal output
      countdown { ExternalWritable := 'False'} : Time;   // Time until next pulse
   END_VAR

   VAR 
      instTofTimePulse {InstructionName := 'TOF_TIME'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : TOF_TIME;   // Timer instance for the Pulse signal (Period of time the clock signal is true)
      statFrequencyOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Real;   // Static Old/Current value of the frequency input
      statTimePulse { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Time;   // Static calculated for me the period of the frequency
   END_VAR

   VAR_TEMP 
      tempImpulse : Bool;   // Temp tag for Pulse output
      tempCountdown : Time;   // Temp tag for count down calculation
   END_VAR

   VAR CONSTANT 
      ZERO : Real := 0.0;   // Numeric zero constant
      ZERO_TIME : Time := T#0ms;   // Time zero constant
      THOUSAND : Real := 1000.0;   // Part of frequency to period formula.
      THOUSAND_SECONDS : Time := t#1000s;   // Part of frequency to period formula.
   END_VAR


BEGIN
	REGION Block info header
	  //=============================================================================
	  // SIEMENS AG / (c)Copyright 2017
	  //-----------------------------------------------------------------------------
	  // Title:            LGF_Impulse
	  // Comment/Function: This function generates pulses at a given frequency. The pulse is always present for one (control) cycle.
	  // Library/Family:   LGF(Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      S7-PLCSIM Advanced V2.0 SP1
	  // Engineering:      TIA Portal V15 Update 2
	  // Restrictions:     ENO disabled - error handling done by error and status
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|------------------------------
	  // 01.00.00 19.08.2015  Siemens Industry Online Support
	  //                      First released version
	  // 01.01.01 16.06.2015  Siemens Industry Online Support
	  //                      LGF_Impulse calls new LGF_Frequency V1.1.1
	  // 01.01.02 02.01.2017  Siemens Industry Online Support
	  //                      Upgrade: TIA Portal V14 Update 1
	  // 01.02.00 02.02.2017  Siemens Industry Online Support
	  //                      Code optimization: no call of LGF_Frequency
	  //                      Fix at output "countdown"
	  // 01.02.01 17.08.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15 Update 2
	  // 01.02.02 23.11.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15.1
	  // 01.02.04 20.09.2019  Simatic Systems Support
	  //                      Code refactoring, regions and more comments added
	  // 03.00.00 23.04.2020  Simatic Systems Support
	  //                      Set version to V3.0.0, harmonize the version of the whole library
	  // 03.00.01 15.02.2021  Simatic Systems Support
	  //                      Insert documentation
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/*
	The function generates pulses at the output `impulse` with the frequency `frequency`.  
	The block always begins with a pulse and sets the next pulse after the period that has elapsed.
	
	#### Example
	
	![LGF_Impulse](LGF_Impulse.png "LGF_Impulse")
	
	*/)
	END_REGION DESCRIPTION
	
	REGION Calculation of settings and program execution
	  // If input <= 0.0 the block is disabled
	  IF #frequency <= #ZERO THEN
	    #tempImpulse := FALSE;
	    #tempCountdown := #ZERO_TIME;
	  ELSE
	    ////Calculating function frequency parameters - only when the input is changed
	    IF #frequency <> #statFrequencyOld THEN
	      // Assign frequency - convert frequency to period
	      #statTimePulse := #THOUSAND_SECONDS / TRUNC(#frequency * #THOUSAND);
	      #statFrequencyOld := #frequency;
	    END_IF;
	    
	    // Assign impulse
	    #tempImpulse := NOT #instTofTimePulse.Q;
	    
	    //Self-restarting timer, with time calculated from the frequency input
	    #instTofTimePulse(IN := NOT #instTofTimePulse.Q,
	                      PT := #statTimePulse);
	    
	    // Assign countdown
	    #tempCountdown := #statTimePulse - #instTofTimePulse.ET;
	  END_IF;
	END_REGION
	
	REGION Writing to outputs
	  // Assigning value of the pulse to the output
	  #impulse := #tempImpulse;
	  #countdown := #tempCountdown;
	  //ENO mechanism is not used - forced to true.
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION_BLOCK

