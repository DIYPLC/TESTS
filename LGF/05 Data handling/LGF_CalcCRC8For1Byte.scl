FUNCTION "LGF_CalcCRC8For1Byte" : Byte
TITLE = LGF_CalcCRC8For1Byte
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Industry_Support
FAMILY : LGF
NAME : LGF_CRC8For1Byte
//The CRC calculation is used for error detection at data transmission. The result of a calculation returns a CRC value via the data sent (Byte). The receiver detects a faulty transmission due to the unequal CRC value. The function `LGF_CalcCRC8For1Byte` uses 8 bits as the generator polynomial (mask).
   VAR_INPUT 
      initValue { S7_PredefinedAssignment := '16#00'} : Byte;   // Start value with which the calculation is executed.  If there is no need for start value - assign 16#00
      mask { S7_PredefinedAssignment := '16#00'} : Byte;   // Generator polynomial with which the calculation is executed.  (Mask / CRC polynomial)
      value : Byte;   // Data byte for which the CRC value will be calculated.
   END_VAR

   VAR_TEMP 
      tempCRC : Byte;   // Temp CRC checksum
      tempIndexCRC : DInt;   // Temp CRC loop index
   END_VAR

   VAR CONSTANT 
      SHIFT_ONE_BIT : UInt := 1;   // Shift the CRC Calculation by 1 bit
      CRC_LOOP_LOWER_LIMIT : DInt := 0;   // Lower limit of CRC loop
      CRC_LOOP_UPPER_LIMIT : DInt := 7;   // Upper limit of CRC loop
   END_VAR


BEGIN
	REGION Block info header
	  //===============================================================================
	  // SIEMENS AG / (c)Copyright 2019
	  //-------------------------------------------------------------------------------
	  // Title:            LGF_CRC8For1Byte
	  // Comment/Function: This function makes a CRC calculation according
	  //                   to the CRC-8 algorithm for 1 byte
	  // Library/Family:   LGF (Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      CPU 1515F-2 PN FW:V2.6
	  // Engineering:      TIA Portal V15.1 Update 2
	  // Restrictions:     ENO disabled - no error handling, no failure possible
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|------------------------------
	  // 01.00.00 03.07.2018  Siemens Industry Online Support
	  //                      First released version
	  // 01.00.01 17.08.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15 Update 2
	  // 01.00.02 23.11.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15.1
	  // 01.00.03 09.10.2019  Simatic Systems Support
	  //                      Code refactoring, comments added
	  // 03.00.00 23.04.2020  Simatic Systems Support
	  //                      Set version to V3.0.0, harmonize the version of the whole library
	  // 03.00.01 12.11.2020  Simatic Systems Support
	  //                      Insert documentation
	  //                      Assign default start values to optional inputs - `initValue`, `mask`
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/*
	The function calculates the CRC value from a data byte `value`. The start value `initValue` and the generator polynomial `mask` can be freely selected.
	
	Note
	:    Various online tools are available for calculating the CRC values. The function of the block was tested with the following online tool, since it supports the input parameters `mask` (Polynomial) and `initValue` (Initial Value):
	:    http://www.sunshine2k.de/coding/javascript/crc/crc_js.html
	*/)
	END_REGION DESCRIPTION
	
	REGION Initialization and input data processing
	  // Defining initial value for algorithm
	  #tempCRC := #initValue XOR #value;
	END_REGION
	
	REGION CRC calculation
	  FOR #tempIndexCRC := #CRC_LOOP_LOWER_LIMIT TO #CRC_LOOP_UPPER_LIMIT DO
	    // Check if MSB is set
	    IF #tempCRC.%X7 THEN
	      // Shift left and perform division by mask polynomial using XOR function
	      #tempCRC := SHL(IN := #tempCRC, N := #SHIFT_ONE_BIT) XOR #mask;
	    ELSE
	      // Shift left without division
	      #tempCRC := SHL(IN := #tempCRC, N := #SHIFT_ONE_BIT);
	    END_IF;
	  END_FOR;
	END_REGION
	
	REGION Outputs assignment
	  #LGF_CalcCRC8For1Byte := #tempCRC;
	  // no error handling needed
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION

