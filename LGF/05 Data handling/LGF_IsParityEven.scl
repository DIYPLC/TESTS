FUNCTION "LGF_IsParityEven" : Bool
TITLE = LGF_IsParityEven
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_SIMATIC_Systems_Support
FAMILY : LGF
NAME : LGF_IsParityEven
//The function checks whether the parity of the input variable of type DWord is even. If the number of bits that are assigned `TRUE` in the sequence is even, the return value is set to `TRUE`.
//
   VAR_INPUT 
      doubleWord : DWord;   // Variable for which the parity is to be determined.
   END_VAR

   VAR_TEMP 
      tempDword : DWord;   // Temp DWord to store and work on the input variable
   END_VAR

   VAR CONSTANT 
      SHIFT_ONE_BIT : USInt := 1;   // Shift constant for one bit
      SHIFT_TWO_BIT : USInt := 2;   // Shift constant for two bits
      SHIFT_TO_RESULT : USInt := 28;   // Shift constant used to shift temp result as lookup table
      MASK_FOR_RESULT : DWord := 16#11111111;   // MASK for parity generation, only those bits needed because of previous shift operations
   END_VAR


BEGIN
	REGION Block info header
	  //============================================================================
	  // SIEMENS AG / (c)Copyright 2018
	  //----------------------------------------------------------------------------
	  // Title:            LGF_IsParityEven
	  // Comment/Function: check if parity in input tag is even
	  //                   if number of bits is even, return TRUE
	  //                   https://graphics.stanford.edu/~seander/bithacks.html#ParityMultiply
	  // Library/Family:   LGF (Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      S7-PLCSIM Advanced V2.0 SP1
	  // Engineering:      TIA Portal V15 Update 2
	  // Restrictions:     ENO not needed, no error possible
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|------------------------------
	  // 01.00.00 | 2019.11.28 | Simatic Systems Support
	  //                         First released version
	  // 03.00.00 | 23.04.2020 | Simatic Systems Support | Set version to V3.0.0
	  //                         harmonize the version of the whole library
	  // 03.00.01 | 12.11.2020 | Simatic Systems Support
	  //                         Insert documentation
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/**/)
	END_REGION DESCRIPTION
	
	REGION Logic
	  #tempDword := #doubleWord; // 32-bit word
	  #tempDword := #tempDword XOR SHR(IN := #tempDword, N := #SHIFT_ONE_BIT);
	  #tempDword := #tempDword XOR SHR(IN := #tempDword, N := #SHIFT_TWO_BIT);
	  #tempDword := UDINT_TO_DWORD(DWORD_TO_UDINT(#tempDword AND #MASK_FOR_RESULT) * DWORD_TO_UDINT(#MASK_FOR_RESULT));
	  
	  #tempDword := SHR(IN := #tempDword, N := #SHIFT_TO_RESULT);
	  
	  // result is stored in LSB
	  #LGF_IsParityEven := NOT #tempDword.%X0;
	  
	  // ENO not needed, no error possible
	  ENO := FALSE;
	END_REGION
	
END_FUNCTION

