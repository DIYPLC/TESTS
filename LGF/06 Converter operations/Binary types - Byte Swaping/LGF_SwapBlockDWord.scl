FUNCTION "LGF_SwapBlockDWord" : Void
TITLE = LGF_SwapBlockDWord
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : 'Siemens Industry Support'
FAMILY : LGF
NAME : LGF_SwapBlockDWord
//Adjusts/ switches the endianness of multibyte data typed values.  
//For this to achieve, a loop will iterate through the array elements and swap the bytes intrinsically.
   VAR_IN_OUT 
      data : Array[*] of DWord;   // Contains the data values, which will be endianness adjusted
   END_VAR

   VAR_TEMP 
      tempUpperIndex : DInt;   // Stores the highest possible array index to end the iterations
      tempSwapIndex : DInt;   // Represents the index used to address the array elements to be swapped
      tempSwapValue : DWord;   // Intermediate storage variable to allow intrinsic byte swap
   END_VAR

   VAR CONSTANT 
      FIRST_DIMENSION : UDInt := 1;   // Indicates the first dimension of the array
      NEXT_ELEMENT : DInt := 1;   // Increment the index to point to the next element
   END_VAR


BEGIN
	REGION BLOCK INFO HEADER
	  //===============================================================================
	  // SIEMENS / (c)Copyright 2025
	  //-------------------------------------------------------------------------------
	  // Title:            LGF_SwapBlockDWord
	  // Function:         Adjusts/ switches the endianness of multibyte data typed values.  
	  //                   For this to achieve, a loop will iterate through the array elements
	  //                   and swap the bytes intrinsically.
	  // Library:          LGF
	  // Author:           Siemens Industry Support
	  // Tested with:      CPU 1214C DC/DC/DC
	  // Engineering:      TIA Portal V20
	  // Restrictions:     --
	  // Requirements:     S7-1200 G2/1500
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|------------------------------
	  // 01.00.00 | 13-10-2021 | Simatic Systems Support| First released version
	  // 01.00.01 | 16-07-2025 | Simatic Systems Support| Fixed comments and block info header
	  // 01.00.02 | 16-07-2025 | Simatic Systems Support| Fixed bug in loop upper index
	  //===============================================================================
	END_REGION
	
	REGION Info
	  // We are going to iterate through the provided array swapping the byte order.
	  // In this procedure we neither care for the endianness of the original system or the 
	  // current target system. This allows a universal use of this block.
	  
	  // Before we can start the iteration, we need to determine at least the lowest index
	  // and the highest index in the provided array.
	  // The lowest index is required to start with the first provided element.
	  // With reaching the highest index we will break the iteration and return from the call
	  // All the values inside the array have been swapped.
	END_REGION
	
	REGION Process
	  // First we set the swapIndex to the lowest possible array index. This is where we start
	  // the swap procedure.
	  #tempSwapIndex := LOWER_BOUND(ARR := #data, DIM := #FIRST_DIMENSION);
	  
	  // Next is to set a temporary variable to the maximum possible index of the array
	  // This we do to avoid continuously calling the systemfunction. This in turn reduces
	  // processing overhead and accelerates the processing.
	  #tempUpperIndex := UPPER_BOUND(ARR := #data, DIM := #FIRST_DIMENSION);
	  
	  // Now we can iterate through all the elements and adjust the byte ordering
	  WHILE (#tempSwapIndex <= #tempUpperIndex) DO
	    // Adjusting the endianness of a multibyte value simply swaps the byte order in the value
	    // A multibyte value can be split into their indivdual bytes. Depending on the systems endianess
	    // the significance of the bytes is different.
	    // At a BigEndian system the significance is highest at the lowest address. The byte order
	    // is: LowSignificanceByte HighSignificanceByte
	    // With LittleEndian systems the significance is highest at the highest address. The byte order is  
	    // reversed to the BigEndian format as: HighSignificanceByte LowSignificanceByte
	    
	    // to swap the order, we first read the value in question into a temporary variable, before
	    // we store it back in reversed byte order.
	    
	    #tempSwapValue := #data[#tempSwapIndex];
	    // We are now swapping the bytes. What was Byte0 becomes Byte3 and vice versa.
	    #data[#tempSwapIndex].%B0 := #tempSwapValue.%B3;
	    #data[#tempSwapIndex].%B1 := #tempSwapValue.%B2;
	    #data[#tempSwapIndex].%B2 := #tempSwapValue.%B1;
	    #data[#tempSwapIndex].%B3 := #tempSwapValue.%B0;
	    
	    // Let's move on to the next value
	    #tempSwapIndex += #NEXT_ELEMENT;
	  END_WHILE;
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION

