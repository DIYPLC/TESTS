FUNCTION_BLOCK "LGF_PulseRelay"
TITLE = LGF_PulseRelay
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Digital_Industry
FAMILY : LGF
NAME : LGF_PulseRelay
//This block corresponds to an impulse relay or a toggle flip-flop including set and reset input.
//Pulse relay, Surge relay, Toggle-Flip-Flop, Frequency divider
//`reset` is leading / prior to `set` or `trigger`
   VAR_INPUT 
      trigger : Bool;   // Trigger to toggle output signal (rising edge)
      set : Bool;   // Set output signal. rising edge
      reset : Bool;   // Reset signal, rising edge (prior to set)
   END_VAR

   VAR_OUTPUT 
      out { ExternalWritable := 'False'} : Bool;   // Ooutput signal
   END_VAR

   VAR 
      statTriggerOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool := false;   // Store old value of trigger input
      statSetOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool := false;   // Store old value of set input
      statResetOld { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool := false;   // Store old value of reset input
      statOut { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool := false;   // Internal output status store to keep the last value in the block
   END_VAR


BEGIN
	REGION BLOCK INFO HEADER
	  //===============================================================================
	  // SIEMENS AG / (c)Copyright 2019
	  //-------------------------------------------------------------------------------
	  // Title:            LGF_PulseRelay
	  // Comment/Function: Pulse relay, Surge relay, Toggle-Flip-Flop, Frequency divider
	  //                   Reset is leading / prior to Set or Trigger
	  // Library/Family:   LGF (Library General Functions)
	  // Author:           Siemens Digital Industry
	  // Tested with:      CPU1212C DC/DC/DC FW:V4.2
	  // Engineering:      TIA Portal V15.1
	  // Restrictions:     ENO not in use, set to true, no error handling
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  // 01.00.00 06.06.2015 Siemens Industry Online Support
	  //                     First released version
	  // 01.00.01 02.01.2017 Siemens Industry Online Support
	  //                     Upgrade: TIA V14 Update 1
	  // 01.00.02 02.01.2017 Siemens Industry Online Support
	  //                     Comment correction                  
	  // 01.00.03 17.08.2018 Siemens Industry Online Support
	  //                     Upgrade: TIA V15 Update 2
	  // 01.00.04 23.11.2018 Siemens Industry Online Support
	  //                     Upgrade: TIA V15.1
	  // 01.00.05 24.05.2019 Simatic Systems Support
	  //                     Refactoring and performance improvement
	  //                     add ENO handling
	  // 03.00.00 23.04.2020 Simatic Systems Support
	  //                     Set version to V3.0.0, harmonize the version of the whole library
	  // 03.00.01 19.01.2021 Simatic Systems Support
	  //                     Insert documentation
	  //===============================================================================
	END_REGION
	
	REGION DESCRIPTION
	  (/*
	Figure: LGF_PulseRelay Signal diagram
	
	![LGF_PulsRelay - Timing diagram](LGF_PulsRelay_timing.PNG "LGF_PulsRelay - Timing diagram")
	
	1. Each rising edge of the input `trigger` changes the Boolean value of the output `out`.
	2. Each rising edge of the input `set` sets the Bboolean value of the output `out` to `TRUE`.
	3. Each rising edge of the input `reset` sets the Boolean value of the output `out` to `FALSE`.
	4. If the inputs `set` and `reset` are set in the same cycle, the `reset` input has priority.
	
	The block can also be used as a frequency divider. If the input `trigger` is supplied with a fixed frequency, the output `out` delivers half the frequency.
	*/)
	END_REGION DESCRIPTION
	
	REGION PROGRAM LOGIC
	  REGION toggle output
	    // Scan for rising edge "trigger"  - toggle static Output
	    IF #trigger AND NOT #statTriggerOld THEN
	      // toggle static output memory
	      #statOut := NOT #statOut;
	    END_IF;
	    // store trigger input for edge dedection
	    #statTriggerOld := #trigger;
	  END_REGION
	  
	  REGION set output
	    // Scan for rising edge "set" - static Output: set
	    IF #set AND NOT #statSetOld THEN
	      // set static output memory
	      #statOut := TRUE;
	    END_IF;
	    // store set input for edge dedection
	    #statSetOld := #set;
	  END_REGION
	  
	  REGION reset output    
	    // Scan for rising edge "reset" - static Output: reset
	    IF #reset AND NOT #statResetOld THEN
	      // store trigger input for edge dedection
	      #statOut := FALSE;
	    END_IF;
	    // store reset input for edge dedection
	    #statResetOld := #reset;
	  END_REGION
	END_REGION
	
	REGION OUTPUTS
	  // write the Output value
	  #out := #statOut;
	  // no error handling needed
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION_BLOCK

