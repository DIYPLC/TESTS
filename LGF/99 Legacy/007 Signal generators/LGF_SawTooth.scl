FUNCTION_BLOCK "LGF_SawTooth"
TITLE = LGF_SawTooth
{ S7_Optimized_Access := 'TRUE' }
AUTHOR : Siemens_Digital_Industries
FAMILY : LFG
NAME : LGF_SawTooth
//This function generates a sawtooth-shaped signal profile. Each sawtooth consists of a defined number of steps (increments).
//
//NOTE
//:    ==LEGACY FUNCTION==
//:    Please update and use the FB with the same name `LGF_CountRisInDWord` in the future!  
//This function is no longer maintained!
   VAR_INPUT 
      startValue : Int;   // Start value at which the signal begins.
      timeRange : Time;   // Time after which the output parameter `value` is incremented
      incrementRange : Int;   // Size of the jump from one increment to the next.
      numberSteps : Int;   // Number of increments per sawtooth. (In the case of an endless sawtooth signal, this information is not necessary).
      endlessSteps : Bool;   // Specifies whether an endless sawtooth signal will be generated.  `TRUE` - Activated, `FALSE` - Disabled
      reset : Bool;   // Sawtooth starts again at the start value, `startValue`.
   END_VAR

   VAR_OUTPUT 
      value { ExternalWritable := 'False'} : Int;   // Current value of the sawtooth signal.
   END_VAR

   VAR 
      statStartValue { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;   // Static copy of input startValue
      statEndlessSteps { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static copy of input endlessSteps
      statTimeRange { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Time;   // Static copy of input timeRange
      statIncrementRange { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;   // Static copy of input incrementRange
      statNumberSteps { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;   // Static copy of input numberSteps
      statCurrentStep { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;   // Static Counter keeping track of steps
      statFirstRun { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool := TRUE;   // Static bit for first execution of the function for initialization
      statStartTimer { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static bit for starting the timer
      statTimerFinished { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Bool;   // Static bit for tracking when the timer has finished
      instTimer {InstructionName := 'TON_TIME'; LibVersion := '1.0'; ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'; S7_SetPoint := 'False'} : TON_TIME;   // Time on-delay instance
      statActValue { ExternalAccessible := 'False'; ExternalVisible := 'False'; ExternalWritable := 'False'} : Int;   // Static Counter for the actual value of the saw tooth signal
   END_VAR

   VAR CONSTANT 
      ZERO : Int;   // Used for initializing/resetting values
      INCREMENT_WITH_ONE : Int := 1;   // Incrementation value
   END_VAR


BEGIN
	REGION Block info header
	  //=============================================================================
	  // SIEMENS AG / (c)Copyright 2017
	  //-----------------------------------------------------------------------------
	  // Library:          LGF_SawTooth
	  // Comment/Function: This function generates a sawtooth-shaped signal profile. Each sawtooth consists of a defined number of steps (increments).
	  // Library/Family:   LGF(Library General Functions)
	  // Author:           Siemens Digital Industry Support
	  // Tested with:      S7-PLCSIM Advanced V2.0 SP1
	  // Engineering:      TIA Portal V15 Update 2
	  // Restrictions:     ENO disabled - no error handling, no failure possible.
	  // Requirements:     PLC (S7-1200 / S7-1500)
	  //-------------------------------------------------------------------------------
	  // Change log table:
	  // Version  | Date       | Expert in charge       | Changes applied
	  //----------|------------|------------------------|------------------------------
	  // 01.00.00 19.08.2015  Siemens Industry Online Support
	  //                      First released version
	  // 01.00.01 02.11.2015  Siemens Industry Online Support
	  //                      Bug fix
	  // 01.00.02 02.01.2017  Siemens Industry Online Support
	  //                      Upgrade: TIA Portal V14 Update 1
	  // 01.00.03 17.08.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15 Update 2
	  // 01.00.04 23.11.2018  Siemens Industry Online Support
	  //                      Upgrade: TIA V15.1
	  // 01.00.10 23.09.2019  Simatic Systems Support
	  //                      Code refactoring, regions and more comments added
	  // 03.00.01 15.02.2021  Simatic Systems Support
	  //                      Insert documentation
	  //=============================================================================
	END_REGION Block info header
	
	REGION DESCRIPTION
	  (/*
	NOTE
	:    Please note that changes at the input parameters only become effective with `reset`.
	
	---
	
	The block calculates the values for a sawtooth-shaped signal profile, which is output to the output parameter `value`. The signal begins with the start value `startValue` and is added with the value `increment` after each elapse of the time interval `timeRange`. The value can also be negative.
	
	If the variable `endlessSteps` is set to `FALSE`, the number of add operations is counted. If this exceeds the value `numberSteps`, the output parameter `value` is set back to the start value. A new sawtooth begins.
	
	If the variable `endlessSteps` is set to `TRUE`, the value `increment` is added without interruption, starting once at `startValue`. If the maximum positive INT value range (32767) of the output parameter `value` is exceeded, `value` changes to the maximum negative INT value range (-32768) and will continue to be added up.
	
	---
	
	NOTE
	:    The duration of a sawtooth at `endlessSteps` on `FALSE` is calculated as follows:
	:    Duration = #timeRange * (#numberSteps + 1)
	
	---
	
	![LGF_SawTooth - Signal profile of the output `value`](LGF_SawTooth.png "LGF_SawTooth - Signal profile of the output `value`")
	*/)
	END_REGION DESCRIPTION
	
	REGION Initialization
	  // Initialization for the first call or reset complete function
	  IF #statFirstRun OR #reset THEN
	    // Copy all input parameter to static values
	    #statStartValue := #startValue;
	    #statTimeRange := #timeRange;
	    #statIncrementRange := #incrementRange;
	    #statNumberSteps := #numberSteps;
	    #statEndlessSteps := #endlessSteps;
	    
	    // Reset static values
	    #statActValue := #statStartValue;
	    #statStartTimer := TRUE;
	    #statCurrentStep := #ZERO;
	    #statFirstRun := FALSE;
	  END_IF;
	END_REGION
	
	REGION Calculation of settings and program execution
	  // Calculation of a new values if the time base is up
	  IF (#statTimerFinished AND NOT #reset) THEN
	    
	    #statActValue := #statActValue + #statIncrementRange;
	    #statCurrentStep := #statCurrentStep + #INCREMENT_WITH_ONE;
	    
	    // If a defined number of steps must be performed 
	    IF (#statEndlessSteps = FALSE) THEN
	      // Restart with start value if number of steps is reached
	      IF #statCurrentStep > #statNumberSteps THEN
	        #statCurrentStep := #ZERO;
	        #statActValue := #statStartValue;
	      END_IF;
	      // If endless is on the step counter is resetted  
	    ELSE
	      #statCurrentStep := #ZERO;
	    END_IF;
	    #statStartTimer := FALSE; // Reset the time base
	  END_IF;
	  
	  // Call a times as basis of the step range
	  #instTimer(IN := #statStartTimer,
	             PT := #statTimeRange,
	             Q  => #statTimerFinished);
	  
	  // Start the timer
	  #statStartTimer := TRUE;
	  
	  // Write Output, if restart is active output value = 0
	  IF (#reset = TRUE) THEN
	    #statActValue := #startValue;
	  END_IF;
	END_REGION
	
	REGION Writing to output
	  #value := #statActValue;
	  // no error handling needed
	  ENO := TRUE;
	END_REGION
	
END_FUNCTION_BLOCK

